use base ("Understand::Codecheck");
use strict;
use constant ERR1 => "Virtual Function called in Constructor";

sub register_tr_text {
  my $check = shift;
  $check->add_tr_text(ERR1);

}



sub checkID { return "CPP_C012";}

sub name { return "All Checks\\Language Specific\\C and C++\\Constructors\\Virtual Function Call In Constructor";}

sub tags {
  return (
    'Language: C',
    'Language: C++',
    'Constructors',
  );
}

sub description {
  return "No virtual functions can be called from constructors . C++ Only";
}

sub detailed_description {
  return "No virtual functions can be called from constructors. C++ Only
  Assumptions:
   1. It is okay (and necessary!) to call virtual functions in non-virtual ways (ie, through an object, not a pointer).
   2. It is okay to call virtual functions that are not in the same class as the constructor.
  Limitations:
   1. False positive identification for calls to local virtual function when it is done through a fully constructed object ".
 "   pointer, not the local this.";
}

sub test_language {
  my $language = shift;
  return $language =~ /C\+\+/;
}

sub test_entity {
  return 1;
}

sub test_global {
  return 0;
}

sub check {
  my $check = shift;
  my $file = shift;
  return unless $file->kind->check('c file');
  my @entrefs = $file->filerefs("define","member function",1);
  foreach my $ref (@entrefs) {
    my $class = $ref->scope;
    my $func = $ref->ent;
    next if !checkConstructorOrDestructor($class,$func);
    foreach my $ref ($func->refs("call","function",1)) {
        next if !checkVirtual($ref);
        if (checkInClass($class,$ref->ent())) {
          if ($func->name() !~ m/~/){
            $check->violation($func,$ref->file,$ref->line,$ref->column,ERR1);
          }
        }
    }
  }
}


# check if func is a constructor or destructor
sub checkConstructorOrDestructor {
    my $class = shift;
    my $func = shift;
    return (($class->name() eq $func->name()));
}


# check if ref is a virtual call
sub checkVirtual {
    my $ref = shift;
    return ($ref->kind->check("virtual"));
}


# check if func is defined in class
sub checkInClass {
    my $class = shift;
    my $func = shift;
    my $ref = $func->ref("definein");
    return ($ref and $ref->ent() == $class);
}
